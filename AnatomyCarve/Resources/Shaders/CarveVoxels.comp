#version 430

#define MAX_SPHERES 32

// workâ€group size; tune to your GPU and volume size
layout (local_size_x = 4, local_size_y = 4, local_size_z = 4) in;

// the sphere details which do the carving. x,y,z are typical position and the w is the radius
uniform vec4 sphereDetails[MAX_SPHERES];
uniform int sphereCount;

// the model matrix of the color volume 
uniform mat4 modelMatrix;

// binding 0: output coloured volume (8-bit RGBA image)
layout(binding = 0, rgba32f) uniform writeonly image3D outputVolume;

// binding 1: input label map (unsigned integer 3D image)
layout(binding = 1, r16ui) uniform readonly uimage3D labelMap;

// binding 2: 
layout(binding = 2, r16) uniform readonly image3D intensityVolume;

// Array of in values where 0 is false and 1 is true that dictates whether or not the label is visible
layout(binding = 3, r8ui) uniform readonly uimage2D mask;

layout(binding = 4, rgba8) uniform readonly image3D colorVolume;


void main() {
    ivec3 dimensions = imageSize(outputVolume);
    ivec3 coord = ivec3(gl_GlobalInvocationID.xyz);

    if (coord.x >= dimensions.x || coord.y >= dimensions.y || coord.z >= dimensions.z)
    {
        return;
    }

    // load the label (uvec4.x holds our label)
    uint label = imageLoad(labelMap, coord).x;

    if (label == 0)
    {
        imageStore(outputVolume, coord, vec4(0, 0, 0, 0));
        return;
    }

    vec4 threeDPoint = modelMatrix * vec4(gl_GlobalInvocationID.xyz, 1.0);
    vec3 pos = (threeDPoint / threeDPoint.w).xyz;

    bool clipped = false;
    for (uint i = 0; i < sphereCount; ++i) 
    {
        uint maskValue = imageLoad(mask, ivec2(label, i)).r;
        bool segmentIsCarved = (maskValue == 0);

        // Clipped for current sphere if the point is inside the sphere
        bool pointInsideSphere = length(pos - sphereDetails[i].xyz) < sphereDetails[i].w;
        bool voxelIsCarved = segmentIsCarved && pointInsideSphere;

        clipped = clipped || voxelIsCarved;
    }
    
    vec4 color = imageLoad(colorVolume, coord);

    if (clipped)
    {
        color.a = 0.0;    
    }
    else
    {
        float intensity = imageLoad(intensityVolume, coord).r;
        color.a = intensity;
    }

    imageStore(outputVolume, coord, color);
}